description = "Apereo CAS central configuration module providing type-safe configuration domain model to the rest of CAS server"

dependencies {
    implementation libraries.springsecurity

    implementation libraries.mongo
    implementation libraries.jasypt
    implementation libraries.groovy
    implementation libraries.bouncycastle
    optional libraries.springbootconfigmetadata

    compileOnly project(":api:cas-server-core-api-authentication")
    compileOnly project(":core:cas-server-core-util")
    compileOnly project(":api:cas-server-core-api-webflow")
    testImplementation project(":core:cas-server-core-util")
}

task generateConfigurationMetadata(type: JavaExec) {
    doFirst {
        println "\nRunning CAS Spring configuration metadata processor, in addition to the native " +
                "Spring Boot metadata processor, to create configuration " +
                "metadata for nested and indexed properties. The process starts with the build directory " +
                "at [$buildDir] for the CAS configuration module at [$projectDir.canonicalPath].\n"
    }

    main = "org.apereo.cas.configuration.ConfigurationMetadataGenerator"
    classpath = sourceSets.main.runtimeClasspath
    args buildDir, projectDir.canonicalPath
}

compileJava.dependsOn(processResources)

if (!Boolean.getBoolean("skipNestedConfigMetadataGen")) {
    jar.dependsOn(generateConfigurationMetadata)
}
